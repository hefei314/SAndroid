plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.hefei.sandroid'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.hefei.sandroid"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a"
        }
    }

    buildTypes {
        debug {
            debuggable true
            // 移除无用的资源文件
            shrinkResources false
            // ZipAlign 优化
            zipAlignEnabled false
            // 设置混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            // 移除无用的资源文件
            shrinkResources true
            // ZipAlign 优化
            zipAlignEnabled true
            // 设置混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    // 执行配置
    applicationVariants.all { variant ->
        // Apk 输出配置
        variant.outputs.all { output ->
            outputFileName = variant.applicationId
            if (variant.buildType.name == buildTypes.debug.getName()) {
                outputFileName += '_' + variant.buildType.name
            }
            outputFileName += '_' + variant.versionName + '_' + variant.versionCode + "_" + buildTime() + '.apk'
        }
    }
}

static def buildTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["junitExt"]
    androidTestImplementation rootProject.ext.dependencies["espressoCore"]

    implementation project(':module_brvah')
    implementation project(':module_common')
    implementation project(':module_matisse')
    implementation project(':module_payment')

    // lombok
    compileOnly rootProject.ext.dependencies["lombok"]
    annotationProcessor rootProject.ext.dependencies["lombok"]

    // 倒计时
    implementation 'com.github.iwgang:countdownview:2.1.6'
    // 骨架屏
    implementation 'com.ethanhua:skeleton:1.1.2'
    implementation 'io.supercharge:shimmerlayout:2.1.0'
    // 闪光效果
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
}